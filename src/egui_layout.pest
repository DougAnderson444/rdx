//! Parse <Horizontal> into egui layout

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

/// Identifier (for tag names and attribute names)
identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | "-")* }

// without quotes or special chars, especially not "(" or ")"
inner_string = @{ (!("\"" | "<" | ">" | "(" | ")") ~ ANY)+ }

/// _SILENT Rule: Doesn't produce Rule:: token. Str literals (with quotes)
string = _{ "\"" ~ inner_string ~ "\"" }

/// Function call
functions = { identifier ~ "(" ~ (string)* ~ ")" }

// Attribute cn be either a string or a function call, but musy have one of them
attribute = { identifier ~ "=" ~ (string | functions) }

/// Non-element contents
text = { (!("<" | ">") ~ ANY)+ }

/// Open Tag such as <Horizontal>
open_tag = { "<" ~ identifier ~ (WHITESPACE* ~ attribute)* ~ ">" }

/// Close Tag such as </Horizontal>
close_tag = { "</" ~ identifier ~ ">" }

/// An entire element, such as <Horizontal> or <Button on_click="increment()">Increment</Button>
element = { open_tag ~ (element | text)* ~ close_tag }

document = _{ SOI ~ element ~ EOI }
