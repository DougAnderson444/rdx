//! Parse <Horizontal> into egui layout

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

/// Identifier (for tag names and attribute names)
identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | "-")* }

/// Function call
functions = { identifier ~ "(" ~ (string)* ~ ")" }

// String literals (without quotes or special chars, especially not "(" or ")")
inner_string = @{ (!("\"" | "<" | ">" | "(" | ")") ~ ANY)+ }

/// String literals (with quotes)
string = _{ "\"" ~ inner_string ~ "\"" }

// Attribute cn be either a string or a function call, but musy have one of them
attribute = { identifier ~ "=" ~ (string | functions) }

/// Tag name such as "Horizontal" or "Button"
text = { (!("<" | ">") ~ ANY)+ }

/// Open Tag such as <Horizontal>
open_tag = { "<" ~ identifier ~ (WHITESPACE* ~ attribute)* ~ ">" }

/// Close Tag such as </Horizontal>
close_tag = { "</" ~ identifier ~ ">" }

/// An entire element, such as <Horizontal> or <Button on_click="increment()">Increment</Button>
element = { open_tag ~ (element | text)* ~ close_tag }

document = { SOI ~ element ~ EOI }
